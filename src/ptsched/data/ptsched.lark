%import common.WS -> _WS
%import common.WS_INLINE -> _WSI
%import common.NEWLINE -> _NEWLINE
%import common.INT -> INT
%import common.DIGIT -> DIGIT

_NEWLINE_BREAK: _WS? _NEWLINE _WS?

schedule: _WS? metadata _NEWLINE_BREAK body _WS?

metadata: _WSI? date _WSI "-" _WSI date _WSI?

date: day_of_month _WSI month _WSI year
day_of_month: /\d{1,2}/
year: /\d{4}/
month: january | february | march | april | may | june | july | august | september | october | november | december
january: "January" | "Jan"
february: "February" | "Feb"
march: "March" | "Mar"
april: "April" | "Apr"
may: "May"
june: "June" | "Jun"
july: "July" | "Jul"
august: "August" | "Aug"
september: "September" | "Sep"
october: "October" | "Oct"
november: "November" | "Nov"
december: "December" | "Dec"

body: (class_ _NEWLINE_BREAK)* class_?

class_: class_declaration _NEWLINE_BREAK day_list
class_declaration: _WSI? "#" _WSI _class_identifier _WSI?
_class_identifier: /[^\n]+/

day_list: (class_day_tasks _NEWLINE_BREAK)* class_day_tasks?

class_day_tasks: date_declaration (_NEWLINE_BREAK task_list)?
date_declaration: _WSI? "-" _WSI _date_specifier _WSI?
_date_specifier: day_of_week _WSI day_of_month
day_of_week: monday | tuesday | wednesday | thursday | friday | saturday | sunday
monday: "Monday" | "Mon"
tuesday: "Tuesday" | "Tue"
wednesday: "Wednesday" | "Wed"
thursday: "Thursday" | "Thu"
friday: "Friday" | "Fri"
saturday: "Saturday" | "Sat"
sunday: "Sunday" | "Sun"

task_list: (task _NEWLINE_BREAK)* task?

task: _WSI? _task_identifier _WSI?
_task_identifier: /[^-#][^\n]*/
